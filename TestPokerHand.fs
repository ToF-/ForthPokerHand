REQUIRE ffl/tst.fs
REQUIRE PokerHand.fs

: TEST-STRING-CARD-RANK-SUIT
    ." a card value can be created from a string." CR
    ." one can extract the rank and suit from a card value." CR
    S" AH" STRING>CARD DUP RANK ACE    ?S SUIT HEARTS ?S
    S" KH" STRING>CARD DUP RANK KING   ?S SUIT HEARTS ?S
    S" QS" STRING>CARD DUP RANK QUEEN  ?S SUIT SPADES ?S
    S" JS" STRING>CARD DUP RANK JACK   ?S SUIT SPADES ?S
    S" TD" STRING>CARD DUP RANK TEN    ?S SUIT DIAMONDS ?S
    S" 9C" STRING>CARD DUP RANK 9      ?S SUIT CLUBS   ?S
    S" 2H" STRING>CARD DUP RANK 2      ?S SUIT HEARTS   ?S
;

: TEST-CARD-CREATION
    ." cards are created as constant words whose name represent the card value." CR
    AH DUP RANK ACE ?S SUIT HEARTS ?S
    1S DUP RANK 1   ?S SUIT SPADES ?S
    KC DUP RANK KING ?S SUIT CLUBS ?S
    QD DUP RANK QUEEN ?S SUIT DIAMONDS ?S
    JS RANK JACK ?S
    AH KH QH JH TH 9H 8H 7H 6H 5H 4H 3H 2H 1H 15 1 DO DUP SUIT HEARTS ?S RANK I ?S LOOP
    AS KS QS JS TS 9S 8S 7S 6S 5S 4S 3S 2S 1S 15 1 DO DUP SUIT SPADES ?S RANK I ?S LOOP
    AD KD QD JD TD 9D 8D 7D 6D 5D 4D 3D 2D 1D 15 1 DO DUP SUIT DIAMONDS ?S RANK I ?S LOOP
    AC KC QC JC TC 9C 8C 7C 6C 5C 4C 3C 2C 1C 15 1 DO DUP SUIT CLUBS ?S RANK I ?S LOOP
;

2 BASE !
: TEST-HAND-CREATION
    0 JH 8D 4S AD 7S 5C KH CARDS>HAND DUP
\ --K   H --5   C --7   S --A   D --4   S --8   D --J   H
  00110100000101110001110100111010000100010010001000101100 ?S
  HAND>CARDS KH ?S 5C ?S 7S ?S AD ?S 4S ?S 8D ?S JH ?S 0 ?S
    0 JH 8D CARDS>HAND DUP
\ --8   D --J   H
  0010001000101100 ?S
  HAND>CARDS 8D ?S JH ?S 0 ?S
;
DECIMAL

VARIABLE MYCOUNTER
: TEST-COUNTER
    ." a counter can count occurrences of numbers from 0 to 14 when they occur between 0 and 7 times" CR
    MYCOUNTER OFF
    3 MYCOUNTER INCREASE
    3 MYCOUNTER COUNT@ 1 ?S
    3 MYCOUNTER INCREASE
    14 MYCOUNTER INCREASE
    14 MYCOUNTER INCREASE
    14 MYCOUNTER INCREASE
    3 MYCOUNTER COUNT@ 2 ?S
    5 MYCOUNTER COUNT@ 0 ?S
    14 MYCOUNTER INCREASE
    14 MYCOUNTER COUNT@ 4 ?S
;

2 BASE !
: TEST-CANCEL-CARD
    ." cancelling 2 cards from a 7 cards hand" CR
    0 JH 8D 4S AD 7S 5C KH CARDS>HAND DUP
\ --K   H --5   C --7   S --A   D --4   S --8   D --J   H
  00110100000101110001110100111010000100010010001000101100 ?S
  0 CANCEL-CARD DUP
\ --K   H --5   C --7   S --A   D --4   S --8   D XXXXXXXX
  00110100000101110001110100111010000100010010001011111111 ?S
  0110 CANCEL-CARD DUP
\ XXXXXXXX--5   C --7   S --A   D --4   S --8   D XXXXXXXX
  11111111000101110001110100111010000100010010001011111111 ?S
  HAND>CARDS
  5C ?S 7S ?S AD ?S 4S ?S 8D ?S 0 ?S
;
DECIMAL

: TEST-COUNT-GROUPS
    ." counting rank groups in a hand" CR
    0 JH 4D 4S AD AS AC KH MYCOUNTER COUNT-GROUPS
    14 MYCOUNTER COUNT@ 3 ?S
    4  MYCOUNTER COUNT@ 2 ?S
    11 MYCOUNTER COUNT@ 1 ?S
    13 MYCOUNTER COUNT@ 1 ?S
    8  MYCOUNTER COUNT@ 0 ?S
;

: TEST-HAND-SIZE
    ." telling the size of a hand" CR
    0 JH 4D 4S AD CARDS>HAND HAND-SIZE 4 ?S
;

: TEST-GROUP-SIZE
    ." setting group size for a card" CR
    3H GROUP-SIZE 1 ?S
    4D 2 GROUP-SIZE!
    GROUP-SIZE 2 ?S
    AH 4 GROUP-SIZE!
    GROUP-SIZE 4 ?S
;

: TEST-HAND-EXTRACTION
   0 JH AC 4S QC 9H CARDS>HAND
   DUP 0 CARD@ JH ?S
   DUP 4 CARD@ 9H ?S
   DUP 3 CARD@ QC ?S
   8S 0 CARD!
   DUP 0 CARD@ 8S ?S
   5H 4 CARD!
   4 CARD@ 5H ?S
;

: TEST-GROUP-SIZES
    0 JH 4S 4C AH AC AD QS CARDS>HAND GROUP-SIZES!
    DUP 0 CARD@ GROUP-SIZE 1 ?S
    DUP 1 CARD@ GROUP-SIZE 2 ?S
    DUP 2 CARD@ GROUP-SIZE 2 ?S
    DUP 3 CARD@ GROUP-SIZE 3 ?S
    DUP 4 CARD@ GROUP-SIZE 3 ?S
    DUP 5 CARD@ GROUP-SIZE 3 ?S
        6 CARD@ GROUP-SIZE 1 ?S
;

: TEST-SORT5CARDS
    ." sorting 5 values on the stack in descending order" CR
    3C AC 8S KS 7C SORT5CARDS
    3C ?S 7C ?S 8S ?S KS ?S AC ?S
;

: TEST-5CARD-HAND
    ." grouping and sorting a 5 card hand" CR
    0 JH AC AS JS JC 5CARD-HAND
    DUP 0 CARD@ DUP RANK 11 ?S GROUP-SIZE 3 ?S
    DUP 1 CARD@ DUP RANK 11 ?S GROUP-SIZE 3 ?S
    DUP 2 CARD@ DUP RANK 11 ?S GROUP-SIZE 3 ?S
    DUP 3 CARD@ DUP RANK 14 ?S GROUP-SIZE 2 ?S
        4 CARD@ DUP RANK 14 ?S GROUP-SIZE 2 ?S
;

: HAND-TEST-SIGNATURE ( hand -- 12345 indexes of cards not cancelled )
    999 7 0 DO
        OVER I CARD@
        255 <> IF
            10 * I +
        THEN
    LOOP NIP ;

: TEST-HAND-TEST-SIGNATURE
    ." signature of hand tells which 5 cards were not removed from 7" CR
    0 JH 8D 4S AD 7S 5C KH CARDS>HAND
    0 CANCEL-CARD 3 CANCEL-CARD
    HAND-TEST-SIGNATURE 99912456 ?S
    0 JH 8D 4S AD 7S 5C KH CARDS>HAND
    1 CANCEL-CARD 6 CANCEL-CARD
    HAND-TEST-SIGNATURE 99902345 ?S
;

: TEST-5CARD-HANDS
    ." generating all 5 card hands from a 7 card hand" CR
    0 JH 8D 4S AD 7S 5C KH CARDS>HAND
    5CARD-HANDS CR
    HAND-TEST-SIGNATURE 99901234 ?S
    HAND-TEST-SIGNATURE 99901235 ?S
    HAND-TEST-SIGNATURE 99901236 ?S
    HAND-TEST-SIGNATURE 99901245 ?S
    HAND-TEST-SIGNATURE 99901246 ?S
    HAND-TEST-SIGNATURE 99901256 ?S
    HAND-TEST-SIGNATURE 99901345 ?S
    HAND-TEST-SIGNATURE 99901346 ?S
    HAND-TEST-SIGNATURE 99901356 ?S
    HAND-TEST-SIGNATURE 99901456 ?S
    HAND-TEST-SIGNATURE 99902345 ?S
    HAND-TEST-SIGNATURE 99902346 ?S
    HAND-TEST-SIGNATURE 99902356 ?S
    HAND-TEST-SIGNATURE 99902456 ?S
    HAND-TEST-SIGNATURE 99903456 ?S
    HAND-TEST-SIGNATURE 99912345 ?S
    HAND-TEST-SIGNATURE 99912346 ?S
    HAND-TEST-SIGNATURE 99912356 ?S
    HAND-TEST-SIGNATURE 99912456 ?S
    HAND-TEST-SIGNATURE 99913456 ?S
    HAND-TEST-SIGNATURE 99923456 ?S
;


TEST-STRING-CARD-RANK-SUIT
TEST-CARD-CREATION
TEST-HAND-CREATION
TEST-COUNTER
TEST-CANCEL-CARD
TEST-COUNT-GROUPS
TEST-HAND-SIZE
TEST-GROUP-SIZE
TEST-HAND-EXTRACTION
TEST-GROUP-SIZES
TEST-SORT5CARDS
TEST-5CARD-HAND
TEST-HAND-TEST-SIGNATURE
TEST-5CARD-HANDS
BYE
